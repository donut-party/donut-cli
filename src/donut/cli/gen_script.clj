(ns donut.cli.gen-script
  (:require
   [babashka.fs :as fs]
   [clojure.edn :as edn]
   [clojure.string :as str]))

(def version-str
  (-> (edn/read-string (slurp "deps.edn"))
      (get-in [:aliases :donut :project :version])))

(def meta-template
  `[(~'ns ~'donut.cli.meta)
    (~'def ~'version
     "This def was generated by the donut-cli build script."
     ~version-str)])

(def meta-str
  (str/join "\n" (map pr-str meta-template)))

(defn gen-script []
  (let [prelude (slurp "prelude")
        new     (slurp "src/donut/cli/new.clj")
        donut   (slurp "src/donut/donut.clj")]
    (spit "donut" (str/join "\n" [prelude meta-str new donut]))
    (fs/set-posix-file-permissions "donut" "rwxr-xr-x")))
